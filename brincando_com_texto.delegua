//FUNÇÕES

//Função para contabilizar caracteres informadas excluindo espaçamentos
funcao contagem_letras(texto) {
    var vetor_palavras = [];

    para var i = 0; i < texto.tamanho(); i++ {
        se texto[i] != ' ' {
            vetor_palavras.adicionar(texto[i]);
        }
    } 

    retorna vetor_palavras.tamanho();
}

//Função para substituir caractere de um texto informado pelo usuário
//Permite que o usuário seleciona a letra a ser substituída e a letra substituta
funcao substituir_letra(texto, original, substituta) {
    var texto_reformulado = "";

    para var i = 0; i < texto.tamanho(); i++ {
        texto_reformulado = texto_reformulado + (texto[i].substituir(original, substituta));
    }

    retorna texto_reformulado;
}

//Função para inverter a ordem das palavras de um texto tendo o espaçamento como delimitador
funcao inverter_palavras(texto) {
    var separar_palavras = texto.dividir(' ');
    var frase_invertida = "";

    para var i = separar_palavras.tamanho() - 1; i >= 0; i = i - 1 {
    frase_invertida = frase_invertida + separar_palavras[i] + " ";
    } 

    retorna frase_invertida;
}

//Função para execução do programa com suas funcionalidades
funcao brincando_com_texto() {
    var texto = leia("Digite uma frase/palavra: ");

    escreva("=================================================");
    escreva("Opção 1: Total de palavras e letras"); 
    escreva("Opção 2: Inverter frase"); 
    escreva("Opção 3: Inverter palavras"); 
    escreva("Opção 4: Alterar uma letra");
    var opcao = leia("O que deseja fazer a seguir? ");


    escreva("=================================================");
    escolha opcao{
        caso '1': 
            var contar_palavras = (texto.dividir(' ')).tamanho();
            var total_letras = contagem_letras(texto);
            escreva("O texto informado possui ${contar_palavras} palavras e ${total_letras} de letras");
        caso '2':
            var invertendo_texto = texto.inverter();
            escreva("Texto invertido: ${invertendo_texto}");
        caso '3':
            var palavras_invertidas = inverter_palavras(texto);
            escreva(palavras_invertidas);
        caso '4':
            var letra_original = leia("Letra a ser alterada: ");
            var letra_substituta = leia("Letra substituta: ");
            var texto_alterado = substituir_letra(texto, letra_original, letra_substituta);
            escreva(texto_alterado);
            
        padrao:
            escreva("Opção inválida");
    }
}

//EXECUÇÃO

//Executando o programa
brincando_com_texto()